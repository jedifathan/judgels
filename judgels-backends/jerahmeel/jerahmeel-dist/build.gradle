plugins {
    id 'com.palantir.sls-java-service-distribution'
    id 'com.palantir.launch-config' version '1.1.0'
}

dependencies {
    runtimeOnly project(':michael:jerahmeel-app')
}

distribution {
    serviceName 'jerahmeel'
    serviceGroup 'judgels'
    mainClass 'judgels.jerahmeel.JerahmeelApplication'
    args 'server', 'var/conf/jerahmeel.yml'
}

task dbStatus(type: JavaExec, dependsOn: jar) {
    group 'Database'
    description 'Checks database migration status.'

    classpath = sourceSets.main.runtimeClasspath
    main = 'judgels.jerahmeel.JerahmeelApplication'
    args = [ 'db', 'status', 'var/conf/jerahmeel.yml' ]
}

task dbMigrate(type: JavaExec, dependsOn: jar) {
    group 'Database'
    description 'Migrates database to the latest version.'

    classpath = sourceSets.main.runtimeClasspath
    main = 'judgels.jerahmeel.JerahmeelApplication'
    args = [ 'db', 'migrate', 'var/conf/jerahmeel.yml' ]
    systemProperty 'liquibase.databaseChangeLogTableName', 'jerahmeel_DATABASECHANGELOG'
    systemProperty 'liquibase.databaseChangeLogLockTableName', 'jerahmeel_DATABASECHANGELOGLOCK'
}

task dbDump(type: JavaExec, dependsOn: jar) {
    group 'Database'
    description 'Dumps current database schema.'

    classpath = sourceSets.main.runtimeClasspath
    main = 'judgels.jerahmeel.JerahmeelApplication'
    args = [ 'db', 'dump', 'var/conf/jerahmeel.yml' ]
}
